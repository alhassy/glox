import expr.{type Expr}
import expr_parser
import parser_combinators.{type Parser, type Span} as parse

/// A Gleam representation of the following grammar.
/// ```
/// program        â†’ statement* EOF 
/// statement      â†’ exprStmt | printStmt 
/// exprStmt       â†’ expression ";"
/// printStmt      â†’ "print" expression ";"
/// ```
pub type Program {
  // Denotes statements `S1; S2; ...; Sn`
  Program(statements: List(Statement))
}

pub type Statement {
  /// Denotes `expression ;`
  ExprStatement(expr: Expr)
  // Denotes `print expr;`
  Print(expr: Expr)
}

const get = parse.then

pub fn parse(source: String) -> parse.ParseResult(Program) {
  source
  |> parse.input_from_string
  |> program()
}

/// A parser for programs
pub fn program() -> Parser(Program) {
  use stmts <- get(statement() |> parse.star())
  use _ <- get(parse.eof())
  parse.return(Program(stmts))
}

// A parser for statements
pub fn statement() -> Parser(Statement) {
  print() |> parse.or(expr())
}

// A parser for `print expr` clauses
pub fn print() -> Parser(Statement) {
  use _ <- get(parse.string("print"))
  use expr <- get(expr_parser.parser())
  use _ <- parse.require(
    parse.string(";"),
    "I expected to see a semicolon here ğŸ¤”",
  )
  parse.return(Print(expr))
}

// A parser for `expr;` clauses
pub fn expr() -> Parser(Statement) {
  use expr <- get(expr_parser.parser())
  use _ <- parse.require(
    parse.string(";"),
    "I expected to see a semicolon here ğŸ¤”",
  )
  parse.return(ExprStatement(expr))
}
